load("@rules_cc//cc:defs.bzl", "cc_binary")
load(
  ":defs.bzl",
  "kernel_build",
  "uboot_build",
  "single_app_cpio_initrd",
  "linux_toolchain",
)
load("//make:make.bzl", "make")

linux_toolchain(
  name = "linux-toolchain-config",
  # TODO: Use select() here to also be compatible with x86_64
  target_cc_toolchain = "@arm_none_linux_gnueabihf//toolchain:cc_toolchain_linux_aarch64_arm",
)

toolchain(
  name = "linux-toolchain",
  toolchain = ":linux-toolchain-config",
  toolchain_type = "//kbuild:toolchain_type",
  target_compatible_with = [
    "@platforms//os:linux",
    "@platforms//cpu:aarch32",
  ],
  visibility = ["//visibility:public"],
)

# NOTE: Can use this to match u-boot and linux toolchains later
# constraint_setting(name = "kbuild_flavor")
#
# constraint_value(
#   name = "kbuild_uboot",
#   constraint_setting = ":kbuild_flavor",
# )

kernel_build(
  name = "linux",
  srcs = ["@linux//:sources"],
  defconfig = "multi_v7_defconfig",
  image = "zImage",
  dtb = "stm32mp157d-dk1.dtb",
  # TODO: Should be possible to pull these from CcToolchain?
  arch = "arm",
  cross_compile = "arm-none-linux-gnueabihf-",
)

uboot_build(
  name = "uboot",
  srcs = ["@u-boot//:sources"],
  defconfig = "stm32mp15_defconfig",
  image = "u-boot.bin",
  # NOTE: Missing extension here is intentional.
  dtb = "stm32mp157d-dk1",
  arch = "arm",
  cross_compile = "arm-none-linux-gnueabihf-",
  # exec_compatible_with = [
  #   ":kbuild_uboot",
  # ]
)

OPTEE_BINARIES = [
  'out/arm-plat-stm32mp1/core/tee-header_v2.bin',
  'out/arm-plat-stm32mp1/core/tee-pager_v2.bin',
  'out/arm-plat-stm32mp1/core/tee-pageable_v2.bin',
]

make(
  name = "optee",
  env = {
    'CROSS_COMPILE': 'arm-none-linux-gnueabihf-',
    'ARCH': 'arm',
    'PLATFORM': 'stm32mp1-157D_DK1',
    'CFG_EMBED_DTB_SOURCE_FILE': 'stm32mp157d-dk1.dts',
  },
  srcs = ["@optee//:sources"],
  outputs = OPTEE_BINARIES,
  target = "all",
  target_compatible_with = [
    "@platforms//os:linux",
    "@platforms//cpu:aarch32",
  ]
)

cc_binary(
  name = "init",
  srcs = ["main.cpp"],
  target_compatible_with = [
    "@platforms//os:linux",
    "@platforms//cpu:aarch32",
  ],
)

# TODO: Put glibc components into a separate target.
single_app_cpio_initrd(
  name = "rootfs",
  init = ":init",
)
